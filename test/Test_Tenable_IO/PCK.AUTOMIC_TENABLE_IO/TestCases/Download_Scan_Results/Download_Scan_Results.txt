*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        download_scan_results
Test Template     Download Scan Results Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScanID            Chapters                                             AssetID        Format     File                                    InitialWaitingTime    RetryCount    RetryInterval
Download Scan Results with valid Scan ID to CSV format
                      [Tags]    main_scenarios
                      OK        ${Scan_ID}        vuln_hosts_summary, vuln_by_host                     ${Asset_ID}    csv        ${File_Path}\\result.csv                0                     5             10

Download Scan Results with valid Scan ID to Nessus format
                      OK        ${Scan_ID}        remediations                                         ${Asset_ID}    nessus     ${File_Path}\\result.nessus             0                     3             10

Download Scan Results with valid Scan ID to HTML format
                      OK        ${Scan_ID}        vuln_by_plugin, compliance                           ${Asset_ID}    html       ${EXECDIR}\\result.html                 0                     4             10

Download Scan Results with valid Scan ID to PDF format
                      OK        ${Scan_ID}        vuln_hosts_summary, vuln_by_host, compliance_exec    ${Asset_ID}    pdf        D:\\Tenable\\testresults\\result.pdf    0                     5             10

Download Scan Results with valid Scan ID to DB format
                      NOK       ${Scan_ID}        remediations                                         ${Asset_ID}    db         D:\\Tenable\\testresults\\result.db     0                     5             10

Download Scan Results with invalid Scan ID
                      NOK       Invalid           vuln_by_host, compliance_exec                        ${Asset_ID}    csv        ${File_Path}\\result1.csv               0                     5             10

Download Scan Results and Validate File as Empty
                      OK        ${Scan_ID}        vuln_by_plugin, compliance                           ${Asset_ID}    html       ${EMPTY}                                0                     5             10

Download Scan Results and Validate AssetID as Empty
                      NOK       ${Scan_ID}        vuln_by_plugin, compliance                           ${EMPTY}       pdf        ${File_Path}\\result.pdf                0                     5             10

Download Scan Results with invalid Chapters field
                      OK        ${Scan_ID}        Invalid                                              ${Asset_ID}    csv        ${EMPTY}                                0                     5             10

Download Scan Results with invalid Format field
                      NOK       ${Scan_ID}        remediations                                         ${Asset_ID}    Invalid    ${EMPTY}                                0                     5             10

Download Scan Results with invalid AssetID
                      OK        ${Scan_ID}        vuln_by_plugin, compliance                           Invalid        pdf        ${File_Path}\\result.pdf                0                     5             10

*** Keywords ***
Startup
    Log    *******Start Test "DOWNLOAD SCAN RESULTS"*******
    Log    *******Connect to AE*******
    Connect AE

Download Scan Results Template
    [Arguments]    ${OK_NOK}    ${ScanID}    ${Chapters}    ${assetID}    ${Format}    ${File}
    ...    ${InitialWaitingTime}    ${RetryCount}    ${RetryInterval}
    [Documentation]    This test downloads scan results from tenable.
    ...    -Precondition: FIle path should be on same server where agent is running.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This field specifies scan id to download results.
    ...    -${Chapters}: This field specifies chapters to download in scan result.
    ...    -${AssetID}: This field specifies ID of the asset scanned.
    ...    -${Format}: This field specifies format to download scan result.
    ...    -${File}: This field specifies where scan results will be download.
    ...    -${InitialWaitingTime}: This field specifies initial waiting time.
    ...    -${RetryCount}: This field specifies the number of retry attempts to get the status.
    ...    -${RetryInterval}: This field specifies the interval in seconds between retry attempt.
    Generate String
    Init Action    ${_DOWNLOAD_SCAN_RESULTS_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Run Keyword If    '${Chapters}' == 'Invalid'    Action Set    &UC4RB_TIO_CHAPTER#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_CHAPTER#    ${Chapters}
    Run Keyword If    '${AssetID}' == 'Invalid'    Action Set    &UC4RB_TIO_ASSET_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_ASSET_ID#    ${AssetID}
    Run Keyword If    '${Format}' == 'Invalid'    Action Set    &UC4RB_TIO_FORMAT#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_FORMAT#    ${Format}
    Run Keyword If    '${File}' == 'Invalid'    Action Set    &UC4RB_TIO_FILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_FILE#    ${File}
    Action Set    &UC4RB_TIO_INIT_WAIT_TIME#    ${InitialWaitingTime}
    Action Set    &UC4RB_TIO_RETRY_CNT#    ${RetryCount}
    Action Set    &UC4RB_TIO_RETRY_INTRVL#    ${RetryInterval}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TIO_SAVED_LOG_FILE_PATH#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TIO_SAVED_LOG_FILE_PATH#

Teardown
    Log    *******End Test "DOWNLOAD SCAN RESULTS"*******
