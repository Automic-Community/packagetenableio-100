*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        check_scan_status
Test Template     Check Scan Status Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScanID            SuccessStatus    FailureStatus       InitialWaitingTime    RetryCount    RetryInterval
Check Scan Status with valid Scan ID
                      [Tags]    main_scenarios
                      OK        ${Scan_ID}        Complete         Aborted,Canceled    30                    50            300

Check Scan Status with invalid Scan ID
                      NOK       Invalid           Complete         Aborted,Canceled    0                     5             10

*** Keywords ***
Startup
    Log    *******Start Test "CHECK SCAN STATUS"*******
    Log    *******Connect to AE*******
    Connect AE

Check Scan Status Template
    [Arguments]    ${OK_NOK}    ${ScanID}    ${SuccessStatus}    ${FailureStatus}    ${InitialWaitingTime}    ${RetryCount}
    ...    ${RetryInterval}
    [Documentation]    This test checks the status for provided scan id.
    ...    -Precondition: FIle path should be on same server where agent is running.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This field specifies scan id to check status.
    ...    -${SuccessStatus}: This field specifies comma seperated status for which action should pass.
    ...    -${FailureStatus}: This field specifies comma seperated status for which action should fail.
    ...    -${InitialWaitingTime}: This field specifies initial waiting time
    ...    -${RetryCount}: This field specifies number of retry attempts to get the status.
    ...    -${RetryInterval}: This field specifies the interval in seconds between retry attempt.
    Generate String
    Init Action    ${_CHECK_SCAN_STATUS_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Action Set    &UC4RB_TIO_SUCCESS_STATUS#    ${SuccessStatus}
    Action Set    &UC4RB_TIO_FAIL_STATUS#    ${FailureStatus}
    Action Set    &UC4RB_TIO_INIT_WAIT_TIME#    ${InitialWaitingTime}
    Action Set    &UC4RB_TIO_RETRY_CNT#    ${RetryCount}
    Action Set    &UC4RB_TIO_RETRY_INTRVL#    ${RetryInterval}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "CHECK SCAN STATUS"*******
