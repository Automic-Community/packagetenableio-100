*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_scan_status
Test Template     Get Scan Status Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScanID
Get Scan Status with valid Scan ID
                      [Tags]    main_scenarios
                      OK        ${Scan_ID}

Get Scan Status with invalid Scan ID
                      NOK       Invalid

Get Scan Status with Empty Scan ID
                      NOK       ${EMPTY}

*** Keywords ***
Startup
    Log    *******Start Test "GET SCAN STATUS"*******
    Log    *******Connect to AE*******
    Connect AE

Get Scan Status Template
    [Arguments]    ${OK_NOK}    ${ScanID}
    [Documentation]    This test will fetch scan status from Tenable.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This field Specifies scan id.
    Generate String
    Init Action    ${_GET_SCAN_STATUS_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TIO_SN_STATUS#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TIO_SN_STATUS#

Teardown
    Log    *******End Test "GET SCAN STATUS"*******
