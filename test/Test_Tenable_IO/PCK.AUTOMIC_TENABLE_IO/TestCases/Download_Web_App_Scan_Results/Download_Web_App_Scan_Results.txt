*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        download_web_app_scan_results
Test Template     Download Web App Scan Results Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK                                                                                ScanID            Chapters                            Format          File                         InitialWaitingTime     RetryCount    RetryInterval
Download Web App Scan Results with valid Scan ID to CSV format
                      [Tags]                                                                                main_scenarios
                      OK                                                                                    ${Scan_ID}        vuln_hosts_summary, vuln_by_host    csv             ${EXECDIR}\\result.csv       0                      5             10

Validate File as Optional field
                      OK                                                                                    ${Scan_ID}        ${EMPTY}                            csv             ${EMPTY}                     0                      5             10

Download Web App Scan Results with valid Scan ID to Nessus format
                      OK                                                                                    ${Scan_ID}        remediations                        nessus          ${EXECDIR}\\result.nessus    0                      5             10
                      # Commenting below test case, as API is giving some error. Issue from Tenable side
                      #Download Web App Scan Results with valid Scan ID to DB format
                      #                                                                                     OK                ${Scan_ID}                          remediations    DB                           E:\\temp\\result.db    0             5                10

Download Web App Scan Results with invalid Scan ID
                      NOK                                                                                   Invalid           vuln_hosts_summary, vuln_by_host    csv             ${EXECDIR}\\result1.csv      0                      5             10

*** Keywords ***
Startup
    Log    *******Start Test "DOWNLOAD WEB APP SCAN RESULTS"*******
    Log    *******Connect to AE*******
    Connect AE

Download Web App Scan Results Template
    [Arguments]    ${OK_NOK}    ${ScanID}    ${Chapters}    ${Format}    ${File}    ${InitialWaitingTime}
    ...    ${RetryCount}    ${RetryInterval}
    [Documentation]    This test downloads web app scan results
    ...    -Precondition: FIle path should be on same server where agent is running.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This field specifies web app scan id to download results.
    ...    -${Chapters}: This field specifies chapters to download in scan result.
    ...    -${Format}: This field specifies format to download scan result.
    ...    -${File}: This field specifies where scan results will be download.
    ...    -${InitialWaitingTime}: This field specifies initial waiting time
    ...    -${RetryCount}: This field specifies number of retry attempts to get the status.
    ...    -${RetryInterval}: This field specifies the interval in seconds between retry attempt.
    Generate String
    Init Action    ${_DOWNLOAD_WEB_APP_SCAN_RESULTS_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Action Set    &UC4RB_TIO_CHAPTER#    ${Chapters}
    Action Set    &UC4RB_TIO_FORMAT#    ${Format}
    Action Set    &UC4RB_TIO_FILE#    ${File}
    Action Set    &UC4RB_TIO_INIT_WAIT_TIME#    ${InitialWaitingTime}
    Action Set    &UC4RB_TIO_RETRY_CNT#    ${RetryCount}
    Action Set    &UC4RB_TIO_RETRY_INTRVL#    ${RetryInterval}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TIO_SAVED_LOG_FILE_PATH#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TIO_SAVED_LOG_FILE_PATH#

Teardown
    Log    *******End Test "DOWNLOAD WEB APP SCAN RESULTS"*******
