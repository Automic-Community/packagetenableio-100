*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        get_agent_group
Test Template     Get Agent Group Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScannerID         Name
Get Agent Group details with valid name
                      [Tags]    main_scenarios
                      OK        ${Scanner_ID}     ${Group_Name}

Get Agent Group details with invalid scanner id
                      NOK       Invalid           ${Group_Name}

Get Agent Group details with invalid name
                      NOK       ${Scanner_ID}     Invalid

*** Keywords ***
Startup
    Log    *******Start Test "GET AGENT GROUP"*******
    Log    *******Connect to AE*******
    Connect AE

Get Agent Group Template
    [Arguments]    ${OK_NOK}    ${ScannerID}    ${Name}
    [Documentation]    This test retrieves information for agent groups.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScannerID}: This field Specifies id of scanner for which you want to fetch agent group.
    ...    -${Name}: This field Specifies name of agent group.
    Generate String
    Init Action    ${_GET_AGENT_GROUP_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScannerID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCANNER_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCANNER_ID#    ${ScannerID}
    Run Keyword If    '${Name}' == 'Invalid'    Action Set    &UC4RB_TIO_AGENT_GRP_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_AGENT_GRP_NAME#    ${Name}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_TIO_AGENT_GRP_UUID#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_TIO_AGENT_GRP_UUID#

Teardown
    Log    *******End Test "GET AGENT GROUP"*******
