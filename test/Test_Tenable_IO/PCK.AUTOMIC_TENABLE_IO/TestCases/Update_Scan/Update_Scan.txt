*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        update_scan
Test Template     Update Scan Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScanID            ScanName    Desc              Folder          Scanner          Enabled       Launch       StartTime        Rules       TimeZone        TargetGroup         AgentGroup         Target        Email        ACL
Update scan while providing valid inputs in all fields
                      [Tags]    main_scenarios
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing Empty Scan ID
                      NOK       ${EMPTY}          ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing neither Target Groups nor Target
                      NOK       ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${EMPTY}            ${Agent_Groups}    ${EMPTY}      ${Emails}    ${ACLs}

Update scan while providing valid mandatory fields inputs and Target Groups
                      OK        ${Scan_ID}        ${EMPTY}    ${EMPTY}          ${EMPTY}        ${EMPTY}         ${Enabled}    ${EMPTY}     ${EMPTY}         ${Rules}    ${EMPTY}        ${Target_Groups}    ${EMPTY}           ${EMPTY}      ${EMPTY}     ${EMPTY}

Update scan while providing valid mandatory fields inputs and Targets
                      OK        ${Scan_ID}        ${EMPTY}    ${EMPTY}          ${EMPTY}        ${EMPTY}         ${Enabled}    ${EMPTY}     ${EMPTY}         ${Rules}    ${EMPTY}        ${EMPTY}            ${EMPTY}           ${Targets}    ${EMPTY}     ${EMPTY}

Update scan while providing invalid Scan ID
                      NOK       Invalid           ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Scan Name
                      OK        ${Scan_ID}        Invalid     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Folder ID
                      NOK       ${Scan_ID}        ${Name}     ${Description}    Invalid         ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Scanner ID
                      NOK       ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    Invalid          ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Launch
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    Invalid      ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Start Time
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    Invalid          ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Rules
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    Invalid     ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Time Zone
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    Invalid         ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Target Groups
                      NOK       ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    Invalid             ${Agent_Groups}    ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Agent Groups
                      NOK       ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    Invalid            ${Targets}    ${Emails}    ${ACLs}

Update scan while providing invalid Targets
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    Invalid       ${Emails}    ${ACLs}

Update scan while providing invalid Emails
                      OK        ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    Invalid      ${ACLs}

Update scan while providing invalid ACLs
                      NOK       ${Scan_ID}        ${Name}     ${Description}    ${Folder_ID}    ${Scanner_ID}    ${Enabled}    ${Launch}    ${Start_Time}    ${Rules}    ${Time_Zone}    ${Target_Groups}    ${Agent_Groups}    ${Targets}    ${Emails}    Invalid

*** Keywords ***
Startup
    Log    *******Start Test "UPDATE SCAN"*******
    Log    *******Connect to AE*******
    Connect AE

Update Scan Template
    [Arguments]    ${OK_NOK}    ${ScanID}    ${ScanName}    ${Desc}    ${Folder}    ${Scanner}
    ...    ${Enabled}    ${Launch}    ${StartTime}    ${Rules}    ${TimeZone}    ${TargetGroup}
    ...    ${AgentGroup}    ${Target}    ${Email}    ${ACL}
    [Documentation]    This test updates scan details in Tenable for the provided scan id.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This fields specifies ID or UUID of a Scan to be updated.
    ...    -${ScanName}: This field specifies a name to the scan.
    ...    -${Desc}: This field specifies a description of the scan.
    ...    -${Folder}: This field specifies the unique ID of the destination folder for the scan.
    ...    -${Scanner}: This field specifies the unique ID of the scanner to use.
    ...    -${Enabled}: This field specifies whether the schedule for the scan is enabled or not.
    ...    -${Launch}: This field specifies when to launch the scan. (i.e. DAILY, WEEKLY, MONTHLY, YEARLY)
    ...    -${StartTime}: This field specifies the starting time and date for the scan (i.e. YYYYMMDDTHHMMSS).
    ...    -${Rules}: This field expects a semi-colon delimited string comprised of three values. The frequency (FREQ=ONETIME or DAILY or WEEKLY or MONTHLY or YEARLY), the interval (INTERVAL=1 or 2 or 3 ... x), and the days of the week (BYDAY=SU,MO,TU,WE,TH,FR,SA). To create a scan that runs every three weeks on Monday Wednesday and Friday the string would be 'FREQ=WEEKLY;INTERVAL=3;BYDAY=MO,WE,FR'
    ...    -${TimeZone}: This field specifies the timezone for the scan schedule.
    ...    -${TargetGroup}: This field specifies list of Target Groups separated by comma to scan.
    ...    -${AgentGroup}: This field specifies list of Agent Groups separated by comma to scan.
    ...    -${Target}: This field specifies list of Targets separated by comma to scan.
    ...    -${Email}: This field specifies comma-separated list of accounts that receive the email summary report.
    ...    -${ACL}: This field specifies a list of permissions to apply to the scan.
    Generate String
    Init Action    ${_UPDATE_SCAN}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Run Keyword If    '${ScanName}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_NAME#    ${ScanName}
    Run Keyword If    '${Folder}' == 'Invalid'    Action Set    &UC4RB_TIO_FOLDER_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_FOLDER_ID#    ${Folder}
    Run Keyword If    '${Scanner}' == 'Invalid'    Action Set    &UC4RB_TIO_SCNR_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCNR_ID#    ${Scanner}
    Run Keyword If    '${Launch}' == 'Invalid'    Action Set    &UC4RB_TIO_LAUNCH#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_LAUNCH#    ${Launch}
    Run Keyword If    '${StartTime}' == 'Invalid'    Action Set    &UC4RB_TIO_STRT_TIME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_STRT_TIME#    ${StartTime}
    Run Keyword If    '${Rules}' == 'Invalid'    Action Set    &UC4RB_TIO_RULES#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_RULES#    ${Rules}
    Run Keyword If    '${TimeZone}' == 'Invalid'    Action Set    &UC4RB_TIO_TIM_ZON#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_TIM_ZON#    ${TimeZone}
    Run Keyword If    '${TargetGroup}' == 'Invalid'    Action Set    &UC4RB_TIO_TRGT_GRPS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_TRGT_GRPS#    ${TargetGroup}
    Run Keyword If    '${AgentGroup}' == 'Invalid'    Action Set    &UC4RB_TIO_AGNT_GRPS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_AGNT_GRPS#    ${AgentGroup}
    Run Keyword If    '${Target}' == 'Invalid'    Action Set    &UC4RB_TIO_TARGETS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_TARGETS#    ${Target}
    Run Keyword If    '${Email}' == 'Invalid'    Action Set    &UC4RB_TIO_EMAILS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_EMAILS#    ${Email}
    Run Keyword If    '${ACL}' == 'Invalid'    Action Set    &UC4RB_TIO_ACLS#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_ACLS#    ${ACL}
    Action Set    &UC4RB_TIO_DESC#    ${Desc}
    Action Set    &UC4RB_TIO_ENABLED#    ${Enabled}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "UPDATE SCAN"*******
