*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        stop_scan
Test Template     Stop Scan Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    ScanID                     WaitForCompletion    InitialWaitingTime    RetryCount    RetryInterval
Stop Scan with valid Scan ID under timeout conditions
                      [Tags]    main_scenarios
                      OK        ${Scan_ID}                 Yes                  0                     5             10

Stop Scan with valid Scan ID with WaitForCompletion as No
                      OK        ${Scan_ID}                 No                   0                     5             10

Stop Scan with valid Scan ID without timeout conditions
                      OK        ${Scan_ID}                 ${EMPTY}             ${EMPTY}              ${EMPTY}      ${EMPTY}

Stop Scan with invalid state Scan ID
                      NOK       ${InvalidState_Scan_ID}    No                   ${EMPTY}              ${EMPTY}      ${EMPTY}

Stop Scan with invalid Scan ID
                      NOK       Invalid                    Yes                  0                     5             10

*** Keywords ***
Startup
    Log    *******Start Test "STOP SCAN"*******
    Log    *******Connect to AE*******
    Connect AE

Stop Scan Template
    [Arguments]    ${OK_NOK}    ${ScanID}    ${WaitForCompletion}    ${InitialWaitingTime}    ${RetryCount}    ${RetryInterval}
    [Documentation]    This test will stop a scan in Tenable.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${ScanID}: This field Specifies scan id to stop.
    ...    -${WaitForCompletion}: This field Specifies whether if action should wait for full stop of the scan..
    ...    -${InitialWaitingTime}: This field Specifies the initial waiting time in seconds before checking for the status.
    ...    -${RetryCount}: This field Specifies the number of repeated checks for the status.
    ...    -${RetryInterval}: This field Specifies the interval in seconds between repeated checks.
    Generate String
    Init Action    ${_STOP_SCAN_ACTION}
    Nessus Authentication
    Run Keyword If    '${ScanID}' == 'Invalid'    Action Set    &UC4RB_TIO_SCAN_ID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_TIO_SCAN_ID#    ${ScanID}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE    Assert Failure

Teardown
    Log    *******End Test "STOP SCAN"*******
